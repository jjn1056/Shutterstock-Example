#!/usr/bin/env perl

## TODO This should probably be a MI Plugin

use warnings;
use strict;
use Config;
use Cwd 'cwd';
use File::Spec;


if(
    ($ENV{PERL_MM_OPT} and 
    ($ENV{MODULEBUILDRC} or $ENV{PERL_MB_OPT}))
) {

    my $which_perl = $ENV{START_SERVER_WHICH_PERL} || $Config{perlpath};
    my $helper_permissions = $ENV{START_SERVER_PERMISSIONS} || '0755';


    my ($install_base, $target) =
        map {split '=', $_} 
        grep { m/^INSTALL_BASE/ }
        split ' ', $ENV{PERL_MM_OPT};

    my $lib = File::Spec->catdir($target, 'lib', 'perl5');
    my $bin = File::Spec->catdir($target, 'bin', 'start-shutterstock-example');

    open(my $bin_fh, '>', $bin)
      or die("Can't open $bin", $!);

    print $bin_fh <<"SCRIPT_CONTENT";
#!$which_perl

BEGIN {
    use Cwd 'cwd';
    use File::Spec;

    use lib File::Spec->catdir(cwd(), 'lib');
    use lib '$lib';
    use local::lib '$target';
}

use strict;
use warnings;
use Plack::Runner;

## If we are installing, should expect to find the psgi file in the bin
## directory, but to make it easier to test we'll also looking in a dev
## script area.

my \$psgi = 'shutterstock-example-web.psgi';
if(-e File::Spec->catdir('$bin', \$psgi)) {
    \$psgi = File::Spec->catdir('$bin', \$psgi);
} elsif(-e File::Spec->catdir(cwd(), 'script', \$psgi)) {
    \$psgi = File::Spec->catdir(cwd(), 'script', \$psgi);
} else {
    die "Can't find your PSGI app file";
}

my \$runner = Plack::Runner->new();
\$runner->parse_options(\@ARGV, \$psgi);
\$runner->run();

SCRIPT_CONTENT

    close($bin_fh);
    chmod oct($helper_permissions), $bin;
} else {
    print <<"MESSAGE";

    Notice: 'start-shutterstock-example' won't be installed since no local::lib
    is active.  This means you are installing the distribution into the global
    Perl lib and you don't need any help.  This is probably not the recommended
    installation method, but I guess you know what you are doing.

MESSAGE
}

